# Copyright 2026 Google LLC.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.


set(PKG_ROOT ${CMAKE_CURRENT_SOURCE_DIR})

# ==============================================================================
# 1. Thread Options
# ==============================================================================
add_litertlm_library(runtime_framework_thread_options INTERFACE)
add_library(LiteRTLM::Framework::ThreadOptions ALIAS runtime_framework_thread_options)

target_include_directories(runtime_framework_thread_options
  INTERFACE
    ${PKG_ROOT}
    ${LITERTLM_INCLUDE_PATHS}
)

# ==============================================================================
# 2. Thread Pool
# ==============================================================================

set(THREADPOOL_SRCS
  threadpool.cc
  worker_thread.cc
)

if(WIN32 OR LITERTLM_USE_STD_THREAD)
  list(APPEND THREADPOOL_SRCS worker_thread_std_thread.cc)
else()
  list(APPEND THREADPOOL_SRCS worker_thread_pthread.cc)
endif()

add_litertlm_library(runtime_framework_threadpool STATIC
  ${THREADPOOL_SRCS}
)
add_library(LiteRTLM::Framework::ThreadPool ALIAS runtime_framework_threadpool)

target_include_directories(runtime_framework_threadpool
  PUBLIC
    ${PKG_ROOT}
    ${LITERTLM_INCLUDE_PATHS}
)

target_link_libraries(runtime_framework_threadpool
  PUBLIC
    LiteRTLM::Framework::ThreadOptions
    LITERTLM_DEPS
)

# ==============================================================================
# 3. Folder Facade
# ==============================================================================
add_library(runtime_framework_libs INTERFACE)
add_library(LiteRTLM::Framework ALIAS runtime_framework_libs)

target_link_libraries(runtime_framework_libs INTERFACE
  LiteRTLM::Framework::ThreadOptions
  LiteRTLM::Framework::ThreadPool
)